# create conda environment
snakemake --use-conda --cluster "qsub -V -cwd -pe smp 8" --jobs 5 -s kraken.smk --keep-going


# kraken_config.yaml
samples : samples.tab


# kraken.smk
import pandas as pd

configfile: 'kraken_config.yaml'

SAMPLE = pd.read_csv(config["samples"], sep="\t").set_index("sample", drop=False)

KRAKEN_CLASSIFICATIONS = expand('kraken_results/{sample}_kraken_classification.txt', sample = SAMPLE.index)

KRAKEN_REPORTS = expand('kraken_results/{sample}_kraken_report.txt', sample=SAMPLE.index)

rule all:
    input:
        KRAKEN_CLASSIFICATIONS, KRAKEN_REPORTS

rule fastp:
    input:
        fwd=lambda wildcards: SAMPLE.loc[wildcards.sample, 'forward'],
        rev=lambda wildcards: SAMPLE.loc[wildcards.sample, 'reverse']
    output:
        fwd='fastp/{sample}_fastp_1.fastq.gz',
        rev='fastp/{sample}_fastp_2.fastq.gz'
    conda:
        'envs/fastp.yaml'
    shell:
        'fastp -i {input.fwd} -I {input.rev} -o {output.fwd} -O {output.rev}'

rule bowtie2:
    input:
        fwd='fastp/{sample}_fastp_1.fastq.gz',
        rev='fastp/{sample}_fastp_2.fastq.gz'
    output:
        fwd='{sample}_unmapped.fastq.1.gz',
        rev='{sample}_unmapped.fastq.2.gz'
    conda:
        'envs/bowtie2.yaml'
    log:
        'logs/bowtie2/{sample}.log'
    shell:
        '(bowtie2 -p 8 -x phiX -1 {input.fwd} -2 {input.rev} --un-conc-gz {wildcards.sample}_unmapped.fastq.gz) 2> {log}'

rule kraken2:
    input:
        fwd='{sample}_unmapped.fastq.1.gz',
        rev='{sample}_unmapped.fastq.2.gz'
    params:
        thread = 8,
        confidence = 0,
        base_qual = 0
    output:
        kraken_class = 'kraken_results/{sample}_kraken_classification.txt',
        kraken_report = 'kraken_results/{sample}_kraken_report.txt'
    conda:
        'envs/kraken2.yaml'
    shell:
        "kraken2 "
        "--db /isilon/saint-hyacinthe-rdc/users/gauthierm/db/kraken2_nt_buildnewdb "
        "--threads {params.thread} "
        "--output {output.kraken_class} "
        "--confidence {params.confidence} "
        "--minimum-base-quality {params.base_qual} "
        "--report {output.kraken_report} "
        #"--memory-mapping "
        "--paired "
        "--use-names "
        "--gzip-compressed "
        "{input.fwd} {input.rev}"
